prototype(Neos.Neos:RawContent) >
prototype(Neos.Neos:RawContent) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Neos.Neos/Private/Templates/RawContentMode/FusionObjects/GeneralContentCollectionRendering.html'
	columns = Neos.Fusion:Collection {
		collection = ${q(node).children('[instanceof Neos.Neos:ContentCollection]')}
		itemName = 'node'
		itemRenderer = Neos.Fusion:Template {
			templatePath = 'resource://Neos.Neos/Private/Templates/RawContentMode/FusionObjects/ContentCollectionTemplate.html'
			node = ${node}
			attributes = Neos.Fusion:Attributes {
				class = 'column'
			}
			columnContent = Neos.Neos:ContentCollection {
				nodePath = '.'
			}
		}
	}

  prototype(Neos.Neos:ContentCase) {
    default {
      condition = Neos.Fusion:CanRender {
        type = ${q(node).property('_nodeType.name')}
      }
    }
    fallback {
      @position = 'after default'
      condition = true
      type = 'Neos.Neos:RawContentFallbackRenderer'
    }
  }
}

prototype(Neos.Neos:RawContentFallbackRenderer) < prototype(Neos.Neos:ContentComponent) {
  label = ${node.label}
  nodeType = ${q(node).property('_nodeType.name')}

  renderer = afx`
    <div>
      <strong>{props.label}</strong>
      <p>Please create a Fusion prototype for <em>{props.nodeType}</em>.</p>
    </div>
  `
}

rawContent = Neos.Neos:Page {
	head {
		stylesheets = Neos.Fusion:Template {
			templatePath = 'resource://Neos.Neos/Private/Templates/RawContentMode/Page/Default.html'
			sectionName = 'headerIncludes'
		}
	}

	bodyTag.attributes.class = 'neos-raw-content-mode'

	body {
		templatePath = 'resource://Neos.Neos/Private/Templates/RawContentMode/Page/Default.html'
		sectionName = 'body'

		allContentCollections = Neos.Neos:PrimaryContent {
			nodePath = '.'
			default.renderer = Neos.Neos:RawContent
		}
	}
}
