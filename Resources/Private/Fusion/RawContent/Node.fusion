
prototype(Neos.Neos:RawContent.Node) < prototype(Neos.Neos:ContentComponent) {
    node = ${node}
    @context.node = ${this.node || node}

    renderer = Neos.Fusion:Case {

        custom {
            @position = "100"
            condition = Neos.Fusion:CanRender {
                type = ${props.node.nodeType.name + '.RawContentMode'}
            }
            renderer = Neos.Fusion:Renderer {
                @context.node = ${props.node}
                type = ${props.node.nodeType.name + '.RawContentMode'}
                element.node = ${props.node}
            }
        }

        defaultContentCollection {
            @position = "200"
            condition = ${q(node).is('[instanceof Neos.Neos:ContentCollection]')}
            renderer = afx`
                <div class="neos-raw-content-contentcollection-container">
                    <header>{props.node.label}</header>
                    <Neos.Neos:RawContent.NodeProperties node={props.node} />
                    <Neos.Neos:RawContent.NodeChildren node={props.node} />
                </div>
            `
        }

        default {
            @position = "300"
            condition = true
            renderer = afx`
                <div>
                    <Neos.Neos:RawContent.NodeProperties node={props.node} />
                    <Neos.Neos:RawContent.NodeChildren node={props.node} />
                </div>
            `
        }
    }
}



