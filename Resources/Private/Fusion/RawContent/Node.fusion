
prototype(Neos.Neos:RawContent.Node) < prototype(Neos.Neos:ContentComponent) {
    node = null
    @context.node = ${this.node}

    renderer = Neos.Fusion:Case {

        custom {
            @position = "100"
            condition = Neos.Fusion:CanRender {
                type = ${node.nodeType.name + '.Raw'}
            }
            renderer = Neos.Fusion:Renderer {
                @context.node = ${props.node}
                type = ${node.nodeType.name + '.Raw'}
            }
        }

        defaultContentCollection {
            @position = "200"
            condition = ${q(props.node).is('[instanceof Neos.Neos:ContentCollection]') || q(props.node).children().is()}
            renderer = afx`
                <div class="neos-raw-content-contentcollection-container">
                    <header><pre>{node.label}</pre></header>
                    <Neos.Neos:RawContent.NodeProperties node={props.node}/>
                    <Neos.Neos:RawContent.NodeChildren node={props.node}/>
                </div>
            `
        }

        default {
            @position = "300"
            condition = true
            renderer = afx`
                <div>
                    <Neos.Neos:RawContent.NodeProperties node={props.node}/>
                    <Neos.Neos:RawContent.NodeChildren node={props.node}/>
                </div>
            `
        }
    }
}



