# Neos.Neos:BreadcrumbMenu provides a breadcrumb navigation based on menu items.
#
prototype(Neos.Neos:BreadcrumbMenu) < prototype(Neos.Fusion:Component) {
  attributes = Neos.Fusion:DataStructure
  maximumLevels = 0
  renderHiddenInIndex = false
  calculateItemStates = false

  @private {
    items = Neos.Neos:BreadcrumbMenuItems {
      maximumLevels = ${props.maximumLevels}
      renderHiddenInIndex = ${props.renderHiddenInIndex}
      calculateItemStates = ${props.calculateItemStates}
    }
  }

  renderer = Neos.Neos:MenuItemListRenderer {
    items = ${private.items}
    attributes = ${props.attributes}
  }

  @exceptionHandler = 'Neos\\Fusion\\Core\\ExceptionHandlers\\ContextDependentHandler'

  @cache {
    mode = 'cached'
    entryIdentifier {
      documentNode = ${Neos.Caching.entryIdentifierForNode(documentNode)}
    }
    entryTags {
      1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
    }
  }
}
