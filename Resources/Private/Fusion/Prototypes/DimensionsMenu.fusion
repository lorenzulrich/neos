# Neos.Neos:DimensionsMenu provides dimension (e.g. language) menu rendering
prototype(Neos.Neos:DimensionsMenu) < prototype(Neos.Fusion:Component) {
  attributes = Neos.Fusion:DataStructure

  @private {
    items = Neos.Neos:DimensionsMenuItems {
      @ignoreProperties = ${['attributes']}
      @apply.props = ${props}
    }
  }

  renderer = Neos.Neos:MenuItemListRenderer {
    items = ${private.items}
    attributes = ${props.attributes}
  }

  @exceptionHandler = 'Neos\\Fusion\\Core\\ExceptionHandlers\\ContextDependentHandler'

  @cache {
    mode = 'cached'
    entryIdentifier {
      documentNode = ${Neos.Caching.entryIdentifierForNode(documentNode)}
    }
    entryTags {
      1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
    }
  }
}

# included for backwards-compatibility only
prototype(Neos.Neos:DimensionMenu) < prototype(Neos.Neos:DimensionsMenu)
