# Neos.Neos:Menu provides basic menu rendering
#
prototype(Neos.Neos:Menu) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Neos.Neos/Private/Templates/FusionObjects/Menu.html'
	node = ${node}

	entryLevel = ${this.startingPoint ? 0 : 1}
	maximumLevels = 2

	filter = 'Neos.Neos:Document'
	attributes = Neos.Fusion:Attributes

	active.attributes = Neos.Fusion:Attributes {
		class = 'active'
	}
	current.attributes = Neos.Fusion:Attributes {
		class = 'current'
	}
	normal.attributes = Neos.Fusion:Attributes {
		class = 'normal'
	}

  @context {
    itemCollection = ${this.itemCollection}
    entryLevel = ${this.entryLevel}
    startingPoint = ${this.startingPoint}
    maximumLevels = ${this.maximumLevels}
    filter = ${this.filter}
    renderHiddenInIndex = ${this.renderHiddenInIndex}
  }

  items = Neos.Neos:MenuData {
    node = ${node}
    itemCollection = ${itemCollection}
    entryLevel = ${entryLevel}
    startingPoint = ${startingPoint}
    maximumLevels = ${maximumLevels}
    filter = ${filter}
    renderHiddenInIndex = ${renderHiddenInIndex}
  }

	@exceptionHandler = 'Neos\\Fusion\\Core\\ExceptionHandlers\\ContextDependentHandler'

	@cache {
		mode = 'cached'
		entryIdentifier {
			documentNode = ${documentNode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
		}
	}
}
